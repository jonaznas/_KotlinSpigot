plugins {
    id "java"
    id "org.jetbrains.kotlin.jvm" version "1.3.61"
    id "com.github.johnrengelman.shadow" version "2.0.4"
    id "net.minecrell.plugin-yml.bukkit" version "0.3.0"
}

group = "change.me"
shadowJar.archiveName = 'plugin.jar'
java.sourceCompatibility = JavaVersion.VERSION_11

sourceSets {
    main {
        kotlin {
            srcDir 'main/kotlin'
        }
        resources {
            srcDir 'main/conf'
        }
    }
}

repositories {
    jcenter()
    mavenCentral()
    maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url 'https://jitpack.io' }
}

dependencies {
    compile group: 'org.jetbrains.kotlinx', name: 'kotlinx-coroutines-core', version: '1.3.3'
    compile group: "org.reflections", name: "reflections", version: "0.9.12"
    compile group: "com.zaxxer", name: "HikariCP", version: "3.4.1"

    compile("org.postgresql:postgresql:42.2.2")
    compile("org.jetbrains.exposed:exposed-core:0.22.1")
    compile("org.jetbrains.exposed:exposed-dao:0.22.1")
    compile("org.jetbrains.exposed:exposed-jdbc:0.22.1")
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.3.61"

    compile "com.github.hazae41:mc-kutils:master-SNAPSHOT"
    compile "org.bukkit:bukkit:1.8.8-R0.1-SNAPSHOT"
    compile "ch.qos.logback:logback-classic:1.2.1"

    shadow "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
}

jar {
    actions = []
    dependsOn = []
    dependsOn('shadowJar')
}

clean {
    group = "_debug"
    delete "debug/server/"
}

task setupDevServer(type: Copy) {
    from "debug/config"
    into "debug/server"
}

task copyPluginToTestserver(dependsOn: ['build'], type: Copy) {
    from "${buildDir}/libs"
    into "debug/server/plugins"
}

task startDevServer(dependsOn: ['copyPluginToTestserver', 'setupDevServer'], type: JavaExec) {
    group = "_debug"
    main = "-jar"
    workingDir = "debug/server/"
    args = ["spigot.jar"]
    standardInput = System.in
}

bukkit {
    main = "change.me.Main"
    name = "KotlinSpigot"
    apiVersion = "1.13"
    load = "STARTUP"
    authors = ['Jonaznas']
    prefix = "KotlinSpigot"

    commands {
        register("abc") {
            description = "This is a test command!"
            usage = "/<command>"
        }
    }
    // more configurations https://github.com/Minecrell/plugin-yml
}
